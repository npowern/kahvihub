apply plugin: 'java'

sourceCompatibility = 1.7

dependencies {
	compile project(':core')
	compile project(':native-plugin')
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    compile 'org.json:json:20090211'
    compile 'commons-cli:commons-cli:1.2'
    compile 'commons-codec:commons-codec:1.4'
    runtime 'org.xerial:sqlite-jdbc:3.8.11.1'
    testRuntime 'org.xerial:sqlite-jdbc:3.8.11.1'
}

task buildNative(type: Exec, description: 'Compile JNI source files') {
	commandLine 'make',
	'-C', '../jni'
}

task cleanNative(type: Exec, description: 'Clean JNI object files') {
	commandLine 'make',
	'-C', '../jni', 
	'clean'
}

clean.dependsOn 'cleanNative'

tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn buildNative
}

version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'Full Java implementation of the IoT Hub',
                   'Implementation-Version': version
    }
}

//create a single Jar with all dependencies
task jarStandalone (type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'Full Java implementation of the IoT Hub',  
        	'Implementation-Version': version,
        	'Main-Class': 'fi.helsinki.cs.iot.kahvihub.KahviHub'
    }
    baseName = 'kahvihub-' + project.name + '-standalone'
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

